# Maintainer: Truemedian <truemedian at gmail dot com>

pkgname='luvi-git'
pkgver=2.11.0.r29.afea1cd
pkgrel=1
pkgdesc="A project in-between luv and luvit"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/luvit/luvi"
license=('Apache')
depends=('libuv' 'pcre' 'openssl')
makedepends=('cmake' 'git')
provides=('luvi')
source=(
	"git+$url.git"
	"fix-build-scripts.patch"
)
sha256sums=(
	'SKIP'
	'1d03935683efccbcd3bad0e9bfbd10ef583c1b8ef8207be76e8cea74e70ed7c2'
)

pkgver() {
	cd "$srcdir/luvi"

	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g;s/^v//')"
}

prepare() {
	cd "$srcdir/luvi"

	patch -p2 -i ../fix-build-scripts.patch

	git submodule init
	git config --remove-section submodule.deps/lua-zlib || true
	git config --remove-section submodule.deps/zlib || true
	git config --remove-section submodule.deps/pcre || true
	git submodule update

	cd "$srcdir/luvi/deps/luv"

	git submodule init
	git config --remove-section submodule.libuv || true
	git config --remove-section submodule.lua || true
	git submodule update

	cd "$srcdir/luvi/deps/lua-openssl"

	git submodule update --init

}

build() {
	cd "$srcdir/luvi"

	make CPACK_FLAGS="-DWITH_SHARED_LIBUV=ON -DWithSharedPCRE=ON" WITHOUT_AMALG=1 regular-shared
	make
}

check() {
	cd "$srcdir/luvi"
	make test
}

package() {
	cd "$srcdir/luvi"

	mkdir -p "$pkgdir/usr/bin"
	make LUVI_PREFIX="$pkgdir/usr" install
}

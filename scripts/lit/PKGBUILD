# Maintainer: Truemedian <truemedian at gmail dot com>

pkgname='lit-git'
pkgver=3.8.4.r7.259ccde
pkgrel=1
pkgdesc="Toolkit for developing, sharing, and running luvit/lua programs and libraries"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/luvit/lit"
license=('Apache')
makedepends=('luvi')
provides=('lit')
source=("git+$url.git")
sha256sums=('SKIP')
options=(!strip)

pkgver() {
    cd "$srcdir/lit"

    printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
    cd "$srcdir/lit"

    git submodule update --init
}

build() {
    cd "$srcdir/lit"

    luvi . -- make . lit "$(which luvi)"
}

package() {
    cd "$srcdir/lit"

    mkdir -p "$pkgdir/usr/bin"
    install -p lit "$pkgdir/usr/bin/lit"
}
